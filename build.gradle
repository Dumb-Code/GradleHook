plugins {
    id 'maven-publish'
    id 'groovy'
    id "com.gradle.plugin-publish" version "0.19.0"
    id "java-gradle-plugin"
    id "org.sonarqube" version "2.7"
}

group 'net.dumbcode.gradlehook'
version '2.0.2'

sourceCompatibility = 1.8

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

processResources.rootSpec.duplicatesStrategy = DuplicatesStrategy.WARN

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.1'
}

gradlePlugin {
    plugins {
        gradleHookPlugin {
            id = 'net.dumbcode.gradlehook'
            implementationClass = 'net.dumbcode.gradlehook.GradleWebhookPlugin'
        }
    }
}

pluginBundle {
    website = 'http://www.dumbcode.net/'
    vcsUrl = 'https://github.com/Dumb-Code/GradleHook'
    description = 'Adds a task to post a build to a webhook'

    plugins() {
        gradleHookPlugin {
            displayName = 'GradleHook'
            version = project.version
            tags = ['webhook', 'integration', 'discord']
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "GradleHook"
        property "sonar.projectKey", "net.dumbcode.gradlehook"

        property "sonar.links.homepage", "https://github.com/Dumb-Code/GradleHook"
        property "sonar.links.ci", "https://travis-ci.com/Dumb-Code/GradleHook"
        property "sonar.links.scm", "https://github.com/Dumb-Code/GradleHook"
        property "sonar.links.issue", "https://github.com/Dumb-Code/GradleHook/issues"
    }
}